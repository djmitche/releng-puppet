@echo off

SET MOZBUILDDIR=C:\mozilla-build
SET MOZILLABUILD=%MOZBUILDDIR%
echo "MozillaBuild directory: %MOZBUILDDIR%"

REM set BUILDBOT_PATH, the path to the active Buildbot virtualenv
call "C:/mozilla-build/bbpath.bat"
echo "Buildbot virtualenv: %BUILDBOT_PATH%"

set log="c:\tmp\buildbot-startup.log"

REM Use the "new" moztools-static
set MOZ_TOOLS=%MOZBUILDDIR%\moztools-x64

REM append moztools to PATH
SET PATH=%PATH%;%MOZ_TOOLS%\bin

cd "%USERPROFILE%"

echo Waiting for puppet to complete

:FileCheck
echo Checking for completetion of PuppetRun.bat.
IF exist <%= @puppet_semaphore %> (GOTO RubyCheck)
echo Puppet run has not completed; waiting
timeout 5
GOTO FileCheck

:RubyCheck
echo Checking that Ruby.exe is no longer running.
tasklist /fi "imagename eq ruby.exe" |find "ruby.exe"
if errorlevel 1 GOTO start
echo Ruby.exe has not completed; waiting
timeout 5
GOTO RubyCheck

:start
echo "%date% %time% - About to run runner.py"

"%MOZILLABUILD%\Python27\python.exe" c:\opt\runner\Scripts\runner.exe -c c:\opt\runner\runner.cfg c:\opt\runner\tasks.d

echo "%date% %time% - runner.py finished"

REM Delete the puppet semaphore so that on reboot we don't think it's started
del <%= @puppet_semaphore %>
